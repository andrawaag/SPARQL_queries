# Endpoint: https://query.wikidata.org/ or https://query.wikidata.org/sparql
# Description: This query gets all properties currently in Wikidata, with its english label, its english description
#               and the affiliated data type.
# Author: Andra Waagmeester

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX schema: <http://schema.org/>
SELECT DISTINCT
    ?property             # the uri of the property
    ?propertyLabel        # its label
    ?propertyDescription  # its description
    ?propType             # its wikibase data type
WHERE {
   ?property wikibase:directClaim ?p ;
             wikibase:propertyType ?propType ;
             schema:description ?propertyDescription ;
             rdfs:label ?propertyLabel .
   FILTER (lang(?propertyLabel) = 'en')
   FILTER (lang(?propertyDescription) = 'en')
}

